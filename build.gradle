plugins {
  id "architectury-plugin" version "3.4-SNAPSHOT"
  id "dev.architectury.loom" version "1.11-SNAPSHOT" apply false
  id "io.github.p03w.machete" version "1.+" // automatic jar compressing on build
  id "me.modmuss50.mod-publish-plugin" version "0.7.4" apply false
}

architectury {
  minecraft = rootProject.minecraft_version
}

subprojects {
  apply plugin: "dev.architectury.loom"
  apply plugin: "me.modmuss50.mod-publish-plugin"
  apply plugin: "maven-publish"

  loom {
    silentMojangMappingsLicense()
  }

  repositories {
    mavenCentral()
    maven { url = "https://maven.shedaniel.me/" } // Cloth Config, REI
    maven { url = "https://maven.blamejared.com/" } // JEI
    maven { url = "https://maven.parchmentmc.org" } // Parchment mappings
    maven { url = "https://maven.quiltmc.org/repository/release" } // Quilt Mappings
    maven { // Flywheel
      url = "https://maven.tterrag.com/"
      content {
        // need to be specific here due to version overlaps
        includeGroup("com.jozufozu.flywheel")
      }
    }
  }

  dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    // layered mappings - Mojmap names, parchment and QM docs and parameters
    mappings(loom.layered {
      it.officialMojangMappings()
      it.parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
    })
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {
        artifactId = rootProject.mod_id + "-" + project.name
        from components.java
      }
    }

    repositories {
      maven {
        name = "Github"
        url = "file://${System.getProperty('user.home')}/Documents/purplecreatemaven"
      }
    }
  }
}

allprojects {
  apply plugin: "java"
  apply plugin: "architectury-plugin"
  apply plugin: "maven-publish"

  archivesBaseName = rootProject.mod_id
  group = rootProject.mod_group_id

  String buildNumber = System.getenv("GITHUB_RUN_NUMBER")
  version = "${mod_version}-mc${minecraft_version}-${project.name}" + (buildNumber != null ? "-${buildNumber}" : "")

  repositories {
    maven { url = "https://maven.neoforged.net/releases/" }
    maven { url = "https://maven.createmod.net/" }
    maven { url = "https://maven.ithundxr.dev/snapshots" }
    //maven { url = "https://mvn.devos.one/snapshots/" }
    //maven { url = "https://mvn.devos.one/releases/" }
    maven { url = "https://api.modrinth.com/maven" }
    maven { url = "https://jitpack.io/" }
    maven { url = "https://maven.jamieswhiteshirt.com/libs-release" }
    maven { url = "https://maven.terraformersmc.com/releases/" }
    maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" }
    maven { url = "https://raw.githubusercontent.com/MisterJulsen/modsrepo/main/maven" }

    //maven { url = "https://maven.theillusivec4.top/" }
  }

  tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
  }

  java {
    withSourcesJar()
  }

  machete {
    // disable machete locally for faster builds
    enabled = buildNumber != null
  }
}

tasks.create("publishTramways") {
  var platform = System.getenv("PLATFORM")

  switch (platform) {
    case "all":
      dependsOn tasks.build, ":fabric:publishMods", ":neoforge:publishMods"
      break
    case "fabric":
    case "neoforge":
      dependsOn "${platform}:build", "${platform}:publishMods"
      break
  }
}

tasks.register('javadocAll', Javadoc) {
  source subprojects.collect { project -> project.sourceSets.main.allJava }
  classpath = files(subprojects.collect { project -> project.sourceSets.main.compileClasspath })
  destinationDir = rootProject.file("docs")

  options {
    encoding = "UTF-8"
  }
}
